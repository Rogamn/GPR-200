#version 300 es

// Attributes
layout (location = 0) in vec4 aPosition;
layout (location = 2) in vec2 aTexCoord;

// Uniforms
uniform mat4 uModelMat, uViewProjMat;
uniform float uTime;

// Varying
out vec2 vTexCoord;

// Calculates a z-axis rotation matrix for a given angle
mat3 zRotation(float angle)
{
	float sinAngle = sin(angle), cosAngle = cos(angle);
	return mat3(cosAngle, sinAngle, 0, -sinAngle, cosAngle, 0, 0, 0, 1);
}

void main()
{
	// Can't edit aPosition, so copy it to new variable
	vec4 position = aPosition;

	// Constantly spin in place
	position.xyz = zRotation(.1 * uTime) * position.xyz;
	
	// Set varying
	vTexCoord = aTexCoord;
	
	// Set gl_Position in clip space
	gl_Position = uViewProjMat * uModelMat * position;
}